TITLE:: ServerRecover
summary:: Strategies for servers to recover when booting shows port conflicts
categories:: Control
related:: Classes/Server


DESCRIPTION::

When trying to boot a server, the server may find that there is already a process
using the desired port number. This can have several reasons, and several strategies.

* It may be a server zombie from a language crash.
One can recover by hijacking that server, or reboot it.

* It may be an scsynth or supernova program run by another application.
One can recover by switching to a different port number.

* In more complex network music settings, one can add further custom
strategies to ServerRecover.

Code examples:

code::

//
// find all server processes on this machine:
ServerRecover.findProcesses;
// for example:
-> [ ( 'username': adc, 'pid': 36998, 'port': 57110 ) ]

// find the pid for the port I want to use
ServerRecover.pidForPort(57110);
// for example:
-> 36998


// first, test regular boot and accidental quit:
// kill all servers first
unixCmd("killall scsynth supernova");
// server address port is free, could boot now
s.addr.portIsFree;
// regular boot, should work fine
s.boot;

// now test killing the server hard
unixCmd("killall scsynth supernova");
s.serverRunning;


// next, demonstrate the zombie problem:

unixCmd("killall scsynth supernova");
// start an scsynth instance by hand to simulate a zombie:
x = (Server.program + s.options.asOptionsString(57110)).unixCmd;
// now server port is used, and regular boot would fail ...
s.addr.portIsFree;

// RECOVER OPTION 1 : 'failAndAsk'
ServerRecover.default.cs;
s.boot;

/********** this posts the other options: **********
// dont do them now, examples follow below!

// ServerRecover 'failAndAsk' for server 'localhost' - options:
// There is already a server using port 57110. You can
// * force-reboot the server:
ServerRecover.default = 'reboot'; s.boot;
// * or find a different port and boot:
ServerRecover.default = 'useFreePort;' s.boot;
// * or hijack the existing server:
ServerRecover.default = 'hijack;' s.boot;
*********************/


// RECOVER OPTION 2: 'hijack'
// - assumes the found server is a zombie from before a lang crash
// and hijacks control of it.
ServerRecover.default = \hijack;
// for annoyance, add a dangling note to the zombie
s.sendMsg("g_new", s.defaultGroup.nodeID, 1, 0);
s.sendMsg("s_new", \default, 1234, 1, 1);
s.queryAllNodes;

// hijacking sends a freeAll command
s.boot;
s.serverRunning; // it knows it is running now
s.pidRunning;    // and has been told its hijacked pid

// simulate server crash:
unixCmd("killall scsynth supernova");
s.serverRunning; // knows it is dead
s.pid;  		// and has no pid anymore


// RECOVER OPTION 3: 'reboot'
// - this is safer: kills the old scsynth, and boots the server anew.

// to test, create zombie first
x = (Server.program + s.options.asOptionsString(57110)).unixCmd;
// add dangling note
s.sendMsg("g_new", s.defaultGroup.nodeID, 1, 0);
s.sendMsg("s_new", \default, 1234, 1, 1);
s.queryAllNodes;
// set to reboot mode, and boot
ServerRecover.default = \reboot; s.boot;


//TODO: \reboot runs and posts TWICE - why?


// RECOVER OPTION 4: 'useFreePort'
// - this is more polite:
// other programs on the same computer may use port 57110,
// for instance to run scsynth instances at the default port.
// 'useFreePort'  finds a free port number, and boots server with that port.
// Switching to a free port number by hand would also work.
unixCmd("killall scsynth supernova");
x = (Server.program + s.options.asOptionsString(57110)).unixCmd;
ServerRecover.default = \useFreePort;
s.boot;
s.addr; // NetAddr(127.0.0.1, 57109)


// bigger test case for multi-servers:
// make sure there are no scsynth processed around
unixCmd("killall scsynth supernova");
(
// Start three servers, as if by other programs, or zombies
x = (Server.program + s.options.asOptionsString(57110)).unixCmd;
x = (Server.program + s.options.asOptionsString(57109)).unixCmd;
x = (Server.program + s.options.asOptionsString(57108)).unixCmd;
)

ServerRecover.default = \useFreePort;
s.boot;
s.addr; // moved port out of the way to NetAddr(127.0.0.1, 57107).


::

CLASSMETHODS::

METHOD:: findProcesses
get info for all running scsynth (or supernova) processes.

METHOD:: pidForPort
get the pid for the given port number, or nil if none found.

METHOD:: bootStrategies
a dictionary of all the strategies

METHOD:: at
argument:: key
get strategy at key, or default.

